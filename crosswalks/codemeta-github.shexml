PREFIX codemeta: <https://w3id.org/codemeta/3.0/>
PREFIX ex: <http://example.org/>
SOURCE repo_info <https://api.github.com/repos/herminiogg/ShExML>
FUNCTIONS helper <scala: https://raw.githubusercontent.com/herminiogg/codemeta-shexml/main/functions.scala>
ITERATOR gh <jsonpath: $> {
    FIELD id <id>
    FIELD name <name>
    FIELD description <description>
    FIELD codeRepository <html_url>
    FIELD language <language>
    FIELD archive_url <archive_url>
    FIELD author <owner.html_url>
    FIELD dateCreated <created_at>
    FIELD dateModified <updated_at>
    FIELD keywords <topics>
    FIELD license <license.url>
    FIELD issueTracker <issues_url>
}
EXPRESSION md <repo_info.gh>

codemeta:SoftwareSourceCode ex:[md.name] {
    a codemeta:SoftwareSourceCode ;
    codemeta:identifier [md.id] ;
    codemeta:name [md.name] ;
    codemeta:description [md.description] ;
    codemeta:codeRepository [md.codeRepository] ;
    codemeta:programmingLanguage [md.language] ;
    codemeta:downloadUrl [md.archive_url] ;
    codemeta:author [md.author] ;
    codemeta:dateCreated [helper.onlyDate(md.dateCreated)] ;
    codemeta:dateModified [helper.onlyDate(md.dateModified)] ;
    codemeta:keywords [md.keywords] ;
    codemeta:license [md.license] ;
    codemeta:issueTracker [md.issueTracker] ;
}
